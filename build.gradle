plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'io.undertree.starter.ysql'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    // upgrade HikariCP to support better maxLifetime variance (see https://github.com/brettwooldridge/HikariCP/pull/2035)
    set('hikaricp.version', '6.2.1')                  // https://github.com/brettwooldridge/HikariCP
    set('flyway.version', '10.22.0')                  // https://github.com/flyway/flyway
    set('flywayTestExtensionsVersion', '10.0.0')      // https://github.com/flyway/flyway-test-extensions
    set('flywayDatabaseYugabyteDBVersion', '10.17.0') // https://github.com/flyway/flyway-community-db-support
    set('hypersistenceUtilsVersion', '3.9.1')         // https://github.com/vladmihalcea/hypersistence-utils
    set('testcontainersVersion', '1.20.4')            // https://github.com/testcontainers/testcontainers-java
    set('yugabyteDriverVersion', '42.7.3-yb-2')       // https://github.com/yugabyte/pgjdbc
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework:spring-aspects'
    implementation "io.hypersistence:hypersistence-utils-hibernate-63:${hypersistenceUtilsVersion}"
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-database-yugabytedb:${flywayDatabaseYugabyteDBVersion}"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    runtimeOnly "com.yugabyte:jdbc-yugabytedb:${yugabyteDriverVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.flywaydb.flyway-test-extensions:flyway-spring-test:${flywayTestExtensionsVersion}"
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:yugabytedb'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named("bootBuildImage") {
    imageName = "ssherwood/yugabytedb-ysql-jpa-starter"
}